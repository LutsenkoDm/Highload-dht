plugins {
    id 'java'
    id 'application'
    id 'net.ltgt.errorprone' version '2.0.2'
    id 'checkstyle'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    // Checks
    errorprone 'com.google.errorprone:error_prone_core:2.15.0'
    checkstyle 'com.puppycrawl.tools:checkstyle:10.3.3'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.0'
    implementation 'ch.qos.logback:logback-classic:1.4.0'
    // As one-nio uses log4j directly
    implementation 'org.slf4j:log4j-over-slf4j:2.0.0'

    // Annotations for better code documentation
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // Our beloved one-nio
    implementation 'ru.odnoklassniki:one-nio:1.5.0'

    // LevelDB
    implementation group: 'org.iq80.leveldb', name: 'leveldb', version: '0.12'

    //implementation 'jdk.incubator.foreign'

    implementation 'org.rocksdb:rocksdbjni:7.5.3'

    // Errorprone annotations for solve with warnings
    implementation group: 'com.google.errorprone', name: 'error_prone_annotations', version: '2.15.0'

    // JUnit Jupiter test framework
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

// Use JDK Incubator Foreign module (for MemorySegment users)
def incubatorArgs = ["--add-modules", "jdk.incubator.foreign"]

application {
    mainClass = 'ok.dht.test.pashchenko.DemoServer'
    applicationDefaultJvmArgs = ['-Xmx128m'] + incubatorArgs
}

test {
    maxHeapSize = "128m"
    useJUnitPlatform()
}

checkstyle {
    configFile = new File("checkstyle.xml")
    checkstyleTest.enabled = false
}

tasks.withType(JavaCompile) {
    options.compilerArgs += incubatorArgs
}

tasks.withType(Test) {
    jvmArgs += incubatorArgs
}